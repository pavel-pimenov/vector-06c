	LXI	D,1000h	;
	MVI	A,10h	;
	DI		;
	CALL	SET_CULIND
	CALL	DISK_SAVE
	EI		;

DS_STATUS	EQU 1BH	; регистр статуса EQU COM
DS_COM		EQU 1BH	; регистр команд и статуса
DS_TRACK	EQU 1AH	; регистр дорожки
DS_SECTOR	EQU 19H	; регистр сектора
DS_DATA		EQU 18H	; регистр данных
DS_MOTOR	EQU 1CH	; запуск двигателя


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;	Процедура осуществляет сброс содержимого буфера	;;
;; в текущий сектор ( буфер должен начинатся с граници	;;
;; блока - младший байт адреса = 00h			;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
DISK_SAVE:
	PUSH	B		; Сохраним регистры.
	PUSH	D		;
	PUSH	H		;
	PUSH	PSW		;
	MVI	A,1		; прочитаем текущий сектор
	OUT	DS_SECTOR	; передать контролеру.
	LXI	D,1000h		;
	MVI	A,10110101b	;
	DI			;
	CALL	PUSK		;
	OUT	DS_COM		;
	CALL	SAVE_SEC	;
	EI			;
	POP	PSW		;
	POP	H		;
	POP	D		;
	POP	B		;
	RET			;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   Процедура выполнения команды в частности использу-	;;
;; ется при шаганиях по диску.				;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
DISK_COMMAND:
	OUT	DS_COM		;
	CALL	DS_WAIT		;
	RET			;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;		Процедура записи сектора.		;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SAVE_SEC:
	LXI	B,0204H		;константы
S_S_LOOP_10:			;
	IN	DS_STATUS	;
	RRC			;
	JNC	S_S_LOOP_10	;
SAV_00:				;
	IN	DS_STATUS	;прочитать статус контролера
	ANA	B		;
	JZ	SAV_00		;
	LDAX	D		;
	OUT	DS_DATA		;
	INR	E		;
	JNZ	SAV_00		;
	INR	D		;
	DCR	C		;
	JNZ	SAV_00		;
	RET			;

;;========================================================
;;процедура установки головы накопителя на указаную дорожку
;;Вход:	А - номер дорожки
;;
SET_CULIND:
	PUSH	PSW		;
	PUSH	PSW		; сохраняем регистр PSW
	CALL	PUSK		; запускаем двигатель на
	MVI	A,00000000B	; текущем накопителе и
	CALL	DISK_COMMAND	;
	POP	PSW		; востанавливаем нужную
	CALL	PUSK		; нам дорожку и производим
	OUT	DS_DATA		; перемещение головы
	MVI	A,00011000B	; 
	CALL	DISK_COMMAND	;
	POP	PSW		;
	RET			; возврат из процедуры
;;========================================================
DS_WAIT:
	PUSH	PSW		;процедура ожидания готовности
DS_WAIT_00:			;
	IN	DS_STATUS	;контролера НГМД
	RRC			;
	JC	DS_WAIT_00	;занято идет исполнение команды
	POP	PSW		;
	RET			;
;;========================================================
PUSK:
	PUSH	PSW		; процедура запуска двигателя
PUSK_00:			;
	LDA	DISK_MASKA	; по заданой маске
	OUT	DS_MOTOR	; в порт управления
	IN	DS_STATUS	;
	RLC			;
	JC	PUSK_00		; если не готов то повторить
	POP	PSW		; востановим флаги
	RET			; возврат
;;
DISK_MASKA:
	DB	34H	;маска выбора рабочего накопителя
			; D2 - бит отвечает за "сторону"
			; диска (при обмене) если этот
			; бит = 1 то выбераются четные
			; дорожки ( по утилите DD.COM )
			; если = 0 то не четные


	END

