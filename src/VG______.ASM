;;
;;    ≥²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²▀
;;    ┼  (C) 1993  Караганда  КптИ  ЭА-91-2  Пименов П.А   ┼
;; ≥²² ²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²²▀  ┼
;; ┼   VGDS.ASM - Пакет продпрограмм для работы с НГМД  ┼  ┼
;; ┼		  на "физическом" ураовне.		┼  ┼
;; ┼ ========== 52-81-95 ========= PPA Systems ======== ┼  ┼
;; ┼             Copyright  by  Пименов Паша            ┼  ┼
;; ┼  ≥²²²²²²²²²²²²²²²²²²▀    ≥²²²²²²²²²²²²²²²²²²²²²²▀  °²²▄
;; ≤²²┴ asm file: 0000h  °²²²²┴ com file: 0000h byte °²²▄
;;    ≤²²²²²²²²²²²²²²²²²²▄    ≤²²²²²²²²²²²²²²²²²²²²²²▄
;;
;;   Все входящие в файл процедуры схраняют все реги-
;; стры процессора !!!
;;
	INCLUDE	VGEQU.ASM

;;______________________________________________________
;;		Описания глобальных процедур		
;;______________________________________________________
;;
	PUBLIC	SET_CULIND
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   Процедура SET_CULIND производит непосредственное 	;;
;; позицирование головок текущего накопителя при этом 	;;
;; не производится контроль на номер текущей  дорожки 	;;
;; т.е  перемещение  возможно по  неотформатированому 	;;
;; диску.						;;
;;  Так как алгоритм установки не опирается на чтение 	;;
;; номера  дорожки с диска то перемещение  происходит 	;;
;; через  "Востановление"  головок ( на 0-й цилиндр )	;;
;; и  последующим  перемещением  головок  на заданный	;;
;; цилиндр при помощи команды "Поиск"  опять  же  без	;;
;; контроля номера дорожки				;;
;;   Процедуру   необходимо  вызывать  при  начальной	;;
;; инициализации контролера и НГМД.			;;
;; Используемые процедуры:				;;
;;	PUSK - Запуск двигателя и выбор накопителя.	;;
;;	DS_WAIT - ожидание готовности контролера.	;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 

	PUBLIC	DS_WAIT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   Процедура DS_WAIT производит ожидание готовности	;;
;; контролера путем циклического чтения байта статуса	;;
;; и анализа младшего бита.				;;
;;   Процедуру необходимо вызывать для ожидания выпол-	;; 
;; олнения какой либо операции.				;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	PUBLIC	PUSK
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   Процедура PUSK производит выбор заданного  нако-	;;
;; пителя (  НГМД  Выбирается  исходя  из содержимого	;;
;; глобальной ячейки DISK_MASKA).			;;
;;   Прочедуру вызывают непосредственно перед началом	;;
;; выполннеия какой либо  операции с диском ( Чтение,	;;
;; запись, и т.д.).					;;
;;   Используемые процедуры:				;;
;;	DS_TIME - Типовая подпрограмма задержки		;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	PUBLIC	DS_TIME
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   Процедура временной задержки. "интервал времени"	;;
;; задается через регистровую пару "BC"			;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;______________________________________________________
;;		Описание глобальных переменных
;;______________________________________________________
;;
	PUBLIC	DISK_MASKA
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   Переменная  DISK_MASKA является байтовым элемен-	;;
;; том управлением выбора накопитель при всех  опера-	;;
;; циях ввода-вывода:					;;
;;	D0 - "0" - Накопитель "А"			;;
;;	     "1" - Накопитель "B"			;;
;;	D2 - "0" - Головка 0				;;
;;	     "1" - Головка 1				;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;========================================================
;;процедура установки головы накопителя на указаную дорожку
;;Вход:	А - номер дорожки
;;
SET_CULIND:
	PUSH	PSW		;
	PUSH	PSW		; сохраняем регистр PSW
	CALL	PUSK		; запускаем двигатель на
	MVI	A,00000000B	; текущем накопителе и
	OUT	DS_COM		; производим востановление
	CALL	DS_WAIT		; головы ( 0-Culinder )
	POP	PSW		; востанавливаем нужную
	CALL	PUSK		; нам дорожку и производим
	OUT	DS_DATA		; перемещение головы
	MVI	A,00011000B	; 
	OUT	DS_COM		;
	CALL	DS_WAIT		; ожидаем готовность
	POP	PSW		;
	RET			; возврат из процедуры
;;========================================================
DS_WAIT:
	PUSH	PSW		;процедура ожидания готовности
DS_WAIT_00:			;
	IN	DS_STATUS	;контролера НГМД
	RRC			;
	JC	DS_WAIT_00	;занято идет исполнение команды
	POP	PSW		;
	RET			;
;;========================================================
PUSK:
	PUSH	PSW		; процедура запуска двигателя
PUSK_00:			;
	LDA	DISK_MASKA	; по заданой маске
	OUT	DS_MOTOR	; в порт управления
	LXI	B,3000H		; константа задержки
	CALL	DS_TIME		; делаем паузу
	IN	DS_STATUS	; читаем статус контролера
	JC	PUSK_00		; если не готов то повторить
	POP	PSW		; востановим флаги
	RET			; возврат
;;
DISK_MASKA:
	DB	00H	;маска выбора рабочего накопителя
;;
;;========================================================
DS_TIME:
	PUSH	PSW		; Типовая программа временной
	PUSH	B		; задержки
DS_T0:				;
	DCX	B		; пустой цикл
	MOV	A,B		; ---//----
	ORA	C		; ---//----
	JNZ	DS_T0		; ---//----
	POP	B		; востанавливает регистры
	POP	PSW		; и возвращаемся
	RET			;
;;========================================================

